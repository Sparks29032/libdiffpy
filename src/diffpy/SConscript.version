import os
import re
import string

Import('env')


def parsemajorminor(hcode):
    'Extract major and minor version from a C++ header file.'
    mx = re.search(r'(?m)^#define *DIFFPY_VERSION_MAJOR *(\d+)', hcode)
    major = int(mx.group(1))
    mx = re.search(r'(?m)^#define *DIFFPY_VERSION_MINOR *(\d+)', hcode)
    minor = int(mx.group(1))
    return (major, minor)


def build_VersionCode(target, source, env):
    tplcode = source[0].get_contents()
    numversion = ginfo['major']
    numversion = 1000 * numversion + ginfo['minor']
    numversion = 1000 * numversion + ginfo['micro']
    numversion = 1000 * numversion + ginfo['patchnumber']
    # verify that formulas in version.tpl work as advertised
    emsg = "Inconsistent value of DIFFPY_VERSION = %i" % numversion
    assert numversion // 1000000000 == ginfo['major'], emsg
    assert numversion // 1000000 % 1000 == ginfo['minor'], emsg
    assert numversion // 1000 % 1000 == ginfo['micro'], emsg
    assert numversion % 500 == ginfo['patchnumber'], emsg
    libversion = str(numversion) + "LL"
    if ginfo['prerelease']:
        libversion = "(-500 + %s)" % libversion
    flds = {
        'DIFFPY_VERSION' : libversion,
        'DIFFPY_VERSION_MAJOR' : ginfo['major'],
        'DIFFPY_VERSION_MINOR' : ginfo['minor'],
        'DIFFPY_VERSION_MICRO' : ginfo['micro'],
        'DIFFPY_VERSION_PATCH' : ginfo['patchnumber'],
        'DIFFPY_VERSION_STR' : ginfo['version'],
        'DIFFPY_VERSION_DATE' : ginfo['date'],
        'DIFFPY_GIT_SHA' : ginfo['commit'],
    }
    versiontemplate = string.Template(tplcode)
    versioncode = versiontemplate.safe_substitute(flds)
    open(target[0].path, 'w').write(versioncode)
    return None

env.Append(BUILDERS={'BuildVersionCode' :
        Builder(action=build_VersionCode, suffix='.hpp', src_suffix='.tpl')})


def build_FeaturesCode(target, source, env):
    tplcode = source[0].get_contents()
    flds = {
        'DIFFPY_HAS_OBJCRYST' : int(env['has_objcryst']),
    }
    codetemplate = string.Template(tplcode)
    codetext = codetemplate.safe_substitute(flds)
    open(target[0].path, 'w').write(codetext)
    return None

env.Append(BUILDERS={'BuildFeaturesCode' :
        Builder(action=build_FeaturesCode, suffix='.hpp', src_suffix='.tpl')})

# Targets --------------------------------------------------------------------

vhpp = File('version.hpp')

MY_GIT_MISSING_ERROR_MSG = """
Cannot determine libdiffpy version.  Compile from a git repository
or use a source archive from

    https://github.com/diffpy/libdiffpy/releases
"""

# If version.hpp exists do not use git
if os.path.isfile(str(vhpp.srcnode())):
    majorminor = parsemajorminor(vhpp.srcnode().get_contents())
else:
    from libdiffpybuildutils import gitinfo
    vtpl = File('version.tpl')
    ginfo = gitinfo()
    if not ginfo:
        print MY_GIT_MISSING_ERROR_MSG
        Exit(1)
    vhpp, = env.BuildVersionCode(['version.hpp'], vtpl)
    env.Depends(vhpp, env.Value(ginfo['version'] + ginfo['commit']))
    majorminor = (ginfo['major'], ginfo['minor'])

fhpp, = env.BuildFeaturesCode(['features.tpl'])
env.Depends(fhpp, env.Value(env['has_objcryst']))

env['lib_includes'] += [vhpp, fhpp]
env['majorminor'] = majorminor

# vim: ft=python

import os
from subprocess import Popen, PIPE

# Do nothing if version.hpp already exists in the source tree.

version_hpp = str(File('version.hpp').srcnode())
if os.path.isfile(version_hpp):
    Return()

# Otherwise build version.hpp from the git log data.

Import('env')
Import('DIFFPY_VERSION_STR')

proc = Popen(['git', 'log', '-1', '--format=%h %at %ai'], stdout=PIPE)
gitlog = proc.stdout.read().strip()

def build_VersionCode(target, source, env):
    import re
    import string
    gsha, gepoch, gisodate = gitlog.split(None, 2)
    flds = {
        'DIFFPY_VERSION' : 0,
        'DIFFPY_VERSION_STR' : DIFFPY_VERSION_STR,
        'DIFFPY_VERSION_DATE' : gisodate,
        'DIFFPY_GITSHA' : gsha,
    }
    major, minor = map(int, DIFFPY_VERSION_STR.split("."))
    revnum = int(gepoch) // 86400
    flds['DIFFPY_VERSION'] = 10000000 * major + 100000 * minor + revnum
    flds['DIFFPY_VERSION_STR'] += "-t" + str(revnum)
    templatecode = open(str(source[0])).read()
    versiontemplate = string.Template(templatecode)
    versioncode = versiontemplate.safe_substitute(flds)
    open(str(target[0]), 'w').write(versioncode)
    return None

env.Append(BUILDERS={'BuildVersionCode' :
        Builder(action=build_VersionCode, suffix='.hpp', src_suffix='.tpl')})

# Targets --------------------------------------------------------------------

ver = env.BuildVersionCode(['version.hpp'], ['version.tpl'])
env.Depends(ver, env.Value([DIFFPY_VERSION_STR, gitlog]))

env['lib_includes'] += ver

# vim: ft=python

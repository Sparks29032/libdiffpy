import os

Import('env', 'libdiffpy')

# Environment for building unit test driver
env_test = env.Clone(CXXTEST_SUFFIX='.hpp')
env_test.Tool('cxxtest')
lib_dir = libdiffpy[0].dir.abspath
env_test.PrependUnique(LIBS='diffpy', LIBPATH=lib_dir)
env_test.PrependENVPath('LD_LIBRARY_PATH', lib_dir)
env_test.PrependENVPath('DYLD_LIBRARY_PATH', lib_dir)

# Targets --------------------------------------------------------------------

def srcincluded(f):
    fl = str(f).lower()
    rv = env.get('has_objcryst') or 'objcryst' not in fl
    if env_test.get('tests') is not None:
        rv = rv and any(t.lower() in fl
                for t in Split(env_test['tests']))
    return rv

# alltests -- the unit test driver source files
test_sources = filter(srcincluded, Glob('Test*.hpp'))

# special builder for tests_dir.cpp
def build_tests_dir(target, source, env):
    tdfullpath = source[0].abspath
    flds = {'tests_dir' : os.path.dirname(tdfullpath)}
    tests_dir_code = open(tdfullpath).read() % flds
    open(str(target[0]), 'w').write(tests_dir_code)
    return None

env_test.Append(BUILDERS={'Build_tests_dir_cpp' :
        Builder(action=build_tests_dir)})
tdcpp = env_test.Build_tests_dir_cpp(['tests_dir.cpp'],
        [env_test.File('tests_dir.tpl').srcnode(),
         env_test.Value(Dir('.').srcnode().abspath)])

test_helpers = [tdcpp, 'python_helpers.cpp', 'objcryst_helpers.cpp']
test_helpers = filter(srcsupported, test_helpers)

alltests = env_test.CxxTest('alltests', test_sources + test_helpers)
Alias('alltests', alltests)

# test -- alias for executing unit tests.
test = env_test.Alias('test', alltests, alltests[0].abspath)
AlwaysBuild(test)

# vim: ft=python
